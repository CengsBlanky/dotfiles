" predefined variables {{{
let vim_config_file="~/.config/nvim/init.vim"
let plugin_path="~/.vim/plugins/"
let plug_file="~/.vim/plug.vim"
let g:python_host_skip_check=1
let g:python3_host_skip_check=1
let g:python3_host_prog = '/usr/local/bin/python3.9'
set guifont=FiraCode\ NF:h18
" }}}
" plugins {{{
execute 'source' plug_file
call plug#begin(fnameescape(plugin_path))
Plug 'git@github.com:tpope/vim-surround.git'
Plug 'git@github.com:tpope/vim-commentary.git'
Plug 'git@github.com:jiangmiao/auto-pairs.git'
Plug 'git@github.com:andymass/vim-matchup.git'
Plug 'git@github.com:justinmk/vim-sneak.git'
Plug 'git@github.com:tpope/vim-fugitive.git'
Plug 'git@github.com:airblade/vim-gitgutter.git'
Plug 'git@github.com:preservim/tagbar.git'
Plug 'git@github.com:lambdalisue/suda.vim.git' " write as root
Plug 'git@github.com:junegunn/vim-easy-align.git' " align text
Plug 'git@github.com:terryma/vim-multiple-cursors.git'
Plug 'git@github.com:preservim/nerdtree.git', { 'on': 'NERDTreeToggle'}
Plug 'git@github.com:neoclide/coc.nvim.git', {'branch': 'release'}
Plug 'git@github.com:romainl/vim-cool.git' " better hlsearch
Plug 'git@github.com:aperezdc/vim-template.git'
Plug 'git@github.com:junegunn/fzf.git', { 'do': { -> fzf#install() } }
Plug 'git@github.com:junegunn/fzf.vim.git'
Plug 'git@github.com:mattn/webapi-vim.git'
Plug 'git@github.com:jsborjesson/vim-uppercase-sql.git', {'for': 'sql'}
Plug 'git@github.com:pangloss/vim-javascript.git'
Plug 'git@github.com:rust-lang/rust.vim.git'
Plug 'git@github.com:fatih/vim-go.git', {'do': ':GoUpdateBinaries'}
Plug 'git@github.com:honza/vim-snippets.git'
Plug 'git@github.com:godlygeek/tabular.git'
Plug 'git@github.com:plasticboy/vim-markdown.git'
Plug 'git@github.com:dstein64/vim-startuptime.git'
Plug 'git@github.com:Yggdroot/indentLine.git' " show indent level
Plug 'git@github.com:ap/vim-buftabline.git'
Plug 'git@github.com:norcalli/nvim-colorizer.lua.git'
Plug 'git@github.com:tribela/vim-transparent.git'
Plug 'git@github.com:arcticicestudio/nord-vim.git'
Plug 'git@github.com:nvim-treesitter/nvim-treesitter.git', {'do': ':TSUpdate'}
Plug 'git@github.com:kyazdani42/nvim-web-devicons.git'
Plug 'git@github.com:ryanoasis/vim-devicons.git'
call plug#end()
" }}}
" plugins setting {{{
" open NERDTreeToggle {{{
let g:NERDTreeQuitOnOpen=3
let NERDTreeIgnore=[
      \ '\.lock$[[file]]', '\.o$[[file]]', '\.out$[[file]]', '\.class$[[file]]', '\.exe$[[file]]',
      \ '^node_modules$[[dir]]', '^dist$[[dir]]', '^packages$[[dir]]', '^target$[[dir]]', '^lib$[[dir]]'
      \ ]
noremap <silent><M-t> :NERDTreeToggle<CR>
" }}}
" tpope/vim-surround {{{
autocmd FileType typescriptreact,javascriptreact nmap t <Plug>YSsurround
" }}}
" auto-pairs {{{
let g:AutoPairsShortcutBackInsert = '<M-b>'
" }}}
" Yggdroot/indentLine {{{
let g:indentLine_fileTypeExclude = ['text', 'json', 'markdown']
" }}}
" airblade/vim-gitgutter {{{
let g:gitgutter_sign_priority = 0
let g:gitgutter_sign_allow_clobber = 0
let g:gitgutter_sign_added = '+'
let g:gitgutter_sign_modified = '*'
let g:gitgutter_sign_removed = '-'
" }}}
" junegunn/vim-easy-align {{{
" Start interactive EasyAlign in visual mode (e.g. vipga)
xmap ga <Plug>(EasyAlign)
" Start interactive EasyAlign for a motion/text object (e.g. gaip)
nmap ga <Plug>(EasyAlign)
" }}}
" terryma/vim-multiple-cursors {{{
let g:multi_cursor_use_default_mapping=0
" Default mapping
let g:multi_cursor_start_word_key      = '<C-n>'
let g:multi_cursor_select_all_word_key = '<M-a>'
let g:multi_cursor_start_key           = 'g<C-n>'
let g:multi_cursor_select_all_key      = 'g<M-a>'
let g:multi_cursor_next_key            = '<C-n>'
let g:multi_cursor_prev_key            = '<C-p>'
let g:multi_cursor_skip_key            = '<C-x>'
let g:multi_cursor_quit_key            = '<Esc>'
let g:multi_cursor_exit_from_visual_mode=1
let g:multi_cursor_exit_from_insert_mode=1
" }}}
" aperezdc/vim-template {{{
let g:templates_directory=["~/.vim/templates/"]
let g:username='zengshuai'
let g:email='zengs1994@gmail.com'
" }}}
" tpope/vim-commentary {{{
augroup commentary_vim
  autocmd!
  autocmd FileType c setlocal commentstring=//\ %s
  autocmd FileType cpp setlocal commentstring=//\ %s
augroup END
" }}}
" neoclide/coc.nvim {{{
let g:coc_config_home="~/.vim/"
" Global extension names to install when they aren't installed.
let g:coc_global_extensions=[
      \ 'coc-json',
      \ 'coc-xml',
      \ 'coc-clangd',
      \ 'coc-java',
      \ 'coc-rls',
      \ 'coc-go',
      \ 'coc-tsserver',
      \ 'coc-pyright',
      \ 'coc-vetur',
      \ 'coc-sh',
      \ 'coc-snippets',
      \ 'coc-prettier',
      \ ]
" Configure the directory which will be used to for coc data
" files(extensions...)
let g:coc_data_home="~/.vim/coc_data/"

" Use tab for trigger completion with characters ahead and navigate.
" NOTE: Use command ':verbose imap <tab>' to make sure tab is not mapped by
" other plugin before putting this into your config.
inoremap <silent><expr> <TAB>
      \ pumvisible() ? "\<C-n>" :
      \ <SID>check_back_space() ? "\<TAB>" :
      \ coc#refresh()
inoremap <expr><S-TAB> pumvisible() ? "\<C-p>" : "\<C-h>"

function! s:check_back_space() abort
  let col = col('.') - 1
  return !col || getline('.')[col - 1]  =~# '\s'
endfunction

" Use <c-space> to trigger completion.
if has('nvim')
  inoremap <silent><expr> <c-space> coc#refresh()
else
  inoremap <silent><expr> <c-@> coc#refresh()
endif

" Make <CR> auto-select the first completion item and notify coc.nvim to
" format on enter, <cr> could be remapped by other vim plugin
inoremap <silent><expr> <cr> pumvisible() ? coc#_select_confirm()
      \: "\<C-g>u\<CR>\<c-r>=coc#on_enter()\<CR>"

" Use `[g` and `]g` to navigate diagnostics
" Use `:CocDiagnostics` to get all diagnostics of current buffer in location list.
nmap <silent> [g <Plug>(coc-diagnostic-prev)
nmap <silent> ]g <Plug>(coc-diagnostic-next)

" GoTo code navigation.
nmap <silent> gd <Plug>(coc-definition)
nmap <silent> gy <Plug>(coc-type-definition)
nmap <silent> gi <Plug>(coc-implementation)
nmap <silent> gr <Plug>(coc-references)

" Use K to show documentation in preview window.
nnoremap <silent> K :call <SID>show_documentation()<CR>

function! s:show_documentation()
  if (index(['vim','help'], &filetype) >= 0)
    execute 'h '.expand('<cword>')
  elseif (coc#rpc#ready())
    call CocActionAsync('doHover')
  else
    execute '!' . &keywordprg . " " . expand('<cword>')
  endif
endfunction

" Highlight the symbol and its references when holding the cursor.
" autocmd CursorHold * silent call CocActionAsync('highlight')

" Symbol renaming.
nmap <leader>n <Plug>(coc-rename)

" Formatting selected code.
" xmap <leader>f  <Plug>(coc-format-selected)
" nmap <leader>f  <Plug>(coc-format-selected)

augroup mygroup
  autocmd!
  " Setup formatexpr specified filetype(s).
  autocmd FileType typescript,json setl formatexpr=CocAction('formatSelected')
  " Update signature help on jump placeholder.
  autocmd User CocJumpPlaceholder call CocActionAsync('showSignatureHelp')
augroup end

" Applying codeAction to the selected region.
" Example: `<leader>aap` for current paragraph
xmap <leader>a  <Plug>(coc-codeaction-selected)
nmap <leader>a  <Plug>(coc-codeaction-selected)

" Remap keys for applying codeAction to the current buffer.
nmap <leader>ac  <Plug>(coc-codeaction)
" Apply AutoFix to problem on the current line.
nmap <leader>qf  <Plug>(coc-fix-current)

" Map function and class text objects
" NOTE: Requires 'textDocument.documentSymbol' support from the language server.
xmap if <Plug>(coc-funcobj-i)
omap if <Plug>(coc-funcobj-i)
xmap af <Plug>(coc-funcobj-a)
omap af <Plug>(coc-funcobj-a)
xmap ic <Plug>(coc-classobj-i)
omap ic <Plug>(coc-classobj-i)
xmap ac <Plug>(coc-classobj-a)
omap ac <Plug>(coc-classobj-a)

" Remap <C-f> and <C-b> for scroll float windows/popups.
if has('nvim-0.4.0') || has('patch-8.2.0750')
  nnoremap <silent><nowait><expr> <C-f> coc#float#has_scroll() ? coc#float#scroll(1) : "\<C-f>"
  nnoremap <silent><nowait><expr> <C-b> coc#float#has_scroll() ? coc#float#scroll(0) : "\<C-b>"
  inoremap <silent><nowait><expr> <C-f> coc#float#has_scroll() ? "\<c-r>=coc#float#scroll(1)\<cr>" : "\<Right>"
  inoremap <silent><nowait><expr> <C-b> coc#float#has_scroll() ? "\<c-r>=coc#float#scroll(0)\<cr>" : "\<Left>"
  vnoremap <silent><nowait><expr> <C-f> coc#float#has_scroll() ? coc#float#scroll(1) : "\<C-f>"
  vnoremap <silent><nowait><expr> <C-b> coc#float#has_scroll() ? coc#float#scroll(0) : "\<C-b>"
endif

" Use CTRL-S for selections ranges.
" Requires 'textDocument/selectionRange' support of language server.
" nmap <silent> <C-s> <Plug>(coc-range-select)
" xmap <silent> <C-s> <Plug>(coc-range-select)

" Add `:Format` command to format current buffer.
command! -nargs=0 Format :call CocAction('format')
" Format whole buffer use Format
" nnoremap <C-f> :Format<CR>
command! -nargs=0 Prettier :CocCommand prettier.formatFile
" Auto format before buffer write
autocmd BufWritePre *.c,*.h,*.cpp :call CocAction('format')

" Add `:Fold` command to fold current buffer.
command! -nargs=? Fold :call     CocAction('fold', <f-args>)

" Add `:OR` command for organize imports of the current buffer.
command! -nargs=0 OR   :call     CocAction('runCommand', 'editor.action.organizeImport')

" Add (Neo)Vim's native statusline support.
" NOTE: Please see `:h coc-status` for integrations with external plugins that
" provide custom statusline: lightline.vim, vim-airline.
set statusline^=%{coc#status()}%{get(b:,'coc_current_function','')}

" Mappings for CoCList
" Show all diagnostics.
nnoremap <silent><nowait> <space>a  :<C-u>CocList diagnostics<cr>
" Manage extensions.
nnoremap <silent><nowait> <space>e  :<C-u>CocList extensions<cr>
" Show commands.
nnoremap <silent><nowait> <space>c  :<C-u>CocList commands<cr>
" Find symbol of current document.
nnoremap <silent><nowait> <space>o  :<C-u>CocList outline<cr>
" Search workspace symbols.
nnoremap <silent><nowait> <space>s  :<C-u>CocList -I symbols<cr>
" Do default action for next item.
nnoremap <silent><nowait> <space>j  :<C-u>CocNext<CR>
" Do default action for previous item.
nnoremap <silent><nowait> <space>k  :<C-u>CocPrev<CR>
" Resume latest coc list.
nnoremap <silent><nowait> <space>p  :<C-u>CocListResume<CR>
" }}}
" rust.vim {{{
let g:rustfmt_autosave = 1
" }}}
" junegunn/fzf {{{
" [Buffers] Jump to the existing window if possible
let g:fzf_buffers_jump = 1
" [[B]Commits] Customize the options used by 'git log':
let g:fzf_commits_log_options = '--graph --color=always --format="%C(auto)%h%d %s %C(black)%C(bold)%cr"'
" [Tags] Command to generate tags file
let g:fzf_tags_command = 'ctags -R'

function! RipgrepFzf(query, fullscreen)
  let command_fmt = 'rg --column --line-number --no-heading  --smart-case -- %s || true'
  let initial_command = printf(command_fmt, shellescape(a:query))
  let reload_command = printf(command_fmt, '{q}')
  let spec = {'options': ['--phony', '--query', a:query, '--bind', 'change:reload:'.reload_command]}
  call fzf#vim#grep(initial_command, 1, fzf#vim#with_preview(spec), a:fullscreen)
endfunction

command! -nargs=* -bang RG call RipgrepFzf(<q-args>, <bang>0)
nnoremap <C-p> :Files<cr>
nnoremap <C-l> :RG<cr>
" }}}
" tagbar {{{
nnoremap <F8> :TagbarToggle<CR><ESC>
let g:rust_use_custom_ctags_defs = 1  " if using rust.vim
let g:tagbar_type_rust = {
  \ 'ctagstype' : 'rust',
  \ 'kinds' : [
      \ 'n:modules',
      \ 's:structures:1',
      \ 'i:interfaces',
      \ 'c:implementations',
      \ 'f:functions:1',
      \ 'g:enumerations:1',
      \ 't:type aliases:1:0',
      \ 'v:constants:1:0',
      \ 'M:macros:1',
      \ 'm:fields:1:0',
      \ 'e:enum variants:1:0',
      \ 'P:methods:1',
  \ ],
  \ 'sro': '::',
  \ 'kind2scope' : {
      \ 'n': 'module',
      \ 's': 'struct',
      \ 'i': 'interface',
      \ 'c': 'implementation',
      \ 'f': 'function',
      \ 'g': 'enum',
      \ 't': 'typedef',
      \ 'v': 'variable',
      \ 'M': 'macro',
      \ 'm': 'field',
      \ 'e': 'enumerator',
      \ 'P': 'method',
  \ },
\ }
" }}}
" buftabline {{{
let g:buftabline_show=1
let g:buftabline_indicators=1
let g:buftabline_numbers=1
" }}}
" vim markdown {{{
let g:vim_markdown_folding_disabled = 1
let g:vim_markdown_conceal = 0
" }}}
" colorscheme plugins {{{
let &t_ut=''
set t_Co=256
set termguicolors
colorscheme nord
" nvim-colorizer {{{
lua <<EOF
require 'colorizer'.setup {
  'css';
  'vue';
  'javascript';
  'html';
}
EOF
" }}}
" highlight works just in certain color name, otherwise color would be normal font color
" set guifg would be enough, but ctermfg would't hurt
augroup color_settings
  autocmd!
  autocmd ColorScheme * highlight Comment ctermfg=Gray guifg=Gray
  autocmd ColorScheme * highlight Visual guibg=#8fbcbb guifg=#000000
  autocmd ColorScheme * highlight Search guifg=#a3be8c guibg=#495057 gui=bold
  autocmd ColorScheme * highlight LineNr guifg=#868e96 gui=none
  autocmd ColorScheme * highlight MatchParen guifg=#ffd43b guibg=none gui=bold
  autocmd ColorScheme * highlight Number gui=none guifg=#a3be8c
  autocmd ColorScheme * highlight String gui=none
  autocmd ColorScheme * highlight Normal guifg=#d8dee9 gui=none

  autocmd ColorScheme * highlight StatusLine gui=bold guifg=#2e3440 guibg=#81a1c1
  autocmd ColorScheme * highlight StatusLineNC gui=none guifg=#d8dee9 guibg=#495057
  autocmd ColorScheme * highlight Folded ctermfg=DarkGrey ctermbg=none guifg=DarkGrey guibg=none

  autocmd ColorScheme * highlight CocUnusedHighlight guifg=#ebcb8b

  autocmd ColorScheme * highlight BufTabLineCurrent guibg=#4c566a guifg=#eceff4 gui=bold
  autocmd ColorScheme * highlight BufTabLineFill guibg=none
  autocmd ColorScheme * highlight BufTabLineHidden guibg=none

  autocmd ColorScheme * highlight GitGutterAdd    guibg=none guifg=#74b816 ctermfg=2 gui=bold
  autocmd ColorScheme * highlight GitGutterChange guibg=none guifg=#fdb924 ctermfg=3 gui=bold
  autocmd ColorScheme * highlight GitGutterDelete guibg=none guifg=#c92a2a ctermfg=1 gui=bold

augroup END

" }}}
" nvim-treesitter {{{
lua <<EOF
require'nvim-treesitter.configs'.setup {
  ensure_installed = { "c", "cpp", "javascript", "typescript", "css", "python", "bash", "html", "toml"}, -- one of "all", "maintained" (parsers with maintainers), or a list of languages
  ignore_install = {}, -- List of parsers to ignore installing
  highlight = {
    enable = true,              -- false will disable the whole extension
    disable = {},  -- list of language that will be disabled
    -- Setting this to true will run `:h syntax` and tree-sitter at the same time.
    -- Set this to `true` if you depend on 'syntax' being enabled (like for indentation).
    -- Using this option may slow down your editor, and you may see some duplicate highlights.
    -- Instead of true it can also be a list of languages
    additional_vim_regex_highlighting = true,
  },
  matchup = {
    enable = true, -- mandatory, false will disable the whole extension
    disable = {},  -- optional, list of language that will be disabled
  }
}
EOF
" }}}
" }}}
" editor {{{
" default {{{
set nocompatible
filetype plugin indent on
syntax on
set fileformat=unix
set fileformats=unix,dos
language en_US.UTF-8
" unicode characters in the file autoload/float.vim
set encoding=utf-8
set fileencoding=utf-8
set fileencodings=ucs-bom,utf-8,cp936
" let mapleader=","
" let maplocalleader=","
inoremap jk <esc>
" TextEdit might fail if hidden is not set.
set hidden
set number
set relativenumber
set backspace=2
set incsearch
set smartcase
set tabstop=4 shiftwidth=4 expandtab smartindent autoindent shiftround
set softtabstop=4
set showcmd
set laststatus=2
set wildmenu
set showmatch
" when file has been chaged outside of vim buffer, autoload it
set autoread
" when switch between buffers or execute specific command save current buffer
set autowrite
set confirm
" Having longer updatetime (default is 4000 ms = 4 s) leads to noticeable
" delays and poor user experience.
set updatetime=100
" set nobackup set nowritebackup set noswapfile set noundofile
set undodir=~/.vim/.undo//
set backupdir=~/.vim/.backup//
set directory=~/.vim/.swp//
set clipboard=unnamed
set clipboard+=unnamedplus
" add ctags support
set tags=tags
" Don't pass messages to |ins-completion-menu|.
set shortmess+=c
" display
set hlsearch
set mousehide
set mouse=nv " enable mouse scroll etc...
set signcolumn=yes
set nowrap
set cmdheight=1
" }}}
" keymappings {{{
" quick quit
nnoremap Q :q<CR>

" quick upper/lower case
nnoremap <leader>] gUiw
nnoremap <leader>[ guiw
" close current window or buffer
noremap <silent><M-w> :close<CR>
noremap <silent><M-b> :bd<CR>
" use command to open my vimrc
command! Editrc execute 'edit' vim_config_file
" screen scroll add <nowait> to execute immediately
" see autocmd keymap_force to set scroll down
" use backspace to scroll up
nnoremap <silent><BS> <C-b>
" <leader> <Enter> to create new line in normal mode
nnoremap <silent><nowait><leader><Enter> :set paste<CR>m`o<ESC>``:set nopaste<CR>
" switch between buffers
nnoremap <silent><nowait><RIGHT> :bn<CR>
nnoremap <silent><nowait><LEFT> :bp<CR>
if exists("bufferline")
  nnoremap <silent><TAB> :BufferNext<CR>
else
  nnoremap <silent><TAB> :bn<CR>
endif
" cd to current file directory
nnoremap <leader>cd :lcd %:p:h<CR>
" map <esc> to quit terminal mode
tnoremap <Esc> <C-\><C-n>
" use <UP> and <DOWN> to scroll screen
nnoremap <silent><UP> 1<C-U><DOWN>
nnoremap <silent><DOWN> 1<C-D><UP>
" save
nnoremap <silent><Enter> :w<CR>
" Execute 'lnoremap x X' and 'lnoremap X x' for each letter a-z.
for c in range(char2nr('A'), char2nr('Z'))
  execute 'lnoremap ' . nr2char(c+32) . ' ' . nr2char(c)
  execute 'lnoremap ' . nr2char(c) . ' ' . nr2char(c+32)
endfor
" Kill the capslock when leaving insert mode.
autocmd InsertLeave * set iminsert=0

"" Open explorer where current file is located {{{
"" Only for win for now.
func! OpenFileExplorer() abort
  if exists("b:netrw_curdir")
    let path = substitute(b:netrw_curdir, "/", "\\", "g")
  elseif expand("%:p") == ""
    let path = expand("%:p:h")
  else
    let path = expand("%:p")
  endif
  " support windows and macos for now
  if has("win32")
    silent exe '!start explorer.exe /select,' .. path
  elseif has('unix')
    silent exe '!open ' .. path
  endif
endfunc

nnoremap <silent> gof :call OpenFileExplorer()<CR>
"}}}

" to use `Meta+{h,j,k,l}` to navigate windows from any mode: {{{
tnoremap <M-h> <C-\><C-N><C-w>h
tnoremap <M-j> <C-\><C-N><C-w>j
tnoremap <M-k> <C-\><C-N><C-w>k
tnoremap <M-l> <C-\><C-N><C-w>l
inoremap <M-h> <C-\><C-N><C-w>h
inoremap <M-j> <C-\><C-N><C-w>j
inoremap <M-k> <C-\><C-N><C-w>k
inoremap <M-l> <C-\><C-N><C-w>l
nnoremap <M-h> <C-w>h
nnoremap <M-j> <C-w>j
nnoremap <M-k> <C-w>k
nnoremap <M-l> <C-w>l
" }}}
" resize window
nnoremap <silent><M-UP> :resize +1<CR>
nnoremap <silent><M-DOWN> :resize -1<CR>
nnoremap <silent><M-LEFT> :vertical resize +1<CR>
nnoremap <silent><M-RIGHT> :vertical resize -1<CR>
" split current window
nnoremap <silent><leader>- :split<CR>
nnoremap <silent><leader>/ :vsplit<CR>
" quickfix list operations
nnoremap <C-j> :cnext<CR>
nnoremap <C-k> :cprevious<CR>
" }}}
" autocmd {{{

augroup filetype_specific
  autocmd!
  autocmd FileType vim setlocal foldmethod=marker
  autocmd FileType html set omnifunc=htmlcomplete#CompleteTags
augroup END

augroup filetype_indent_size
  autocmd!
  autocmd FileType html,css,javascript,typescript,typescriptreact,vue,yaml,sql,json,vim
        \ setlocal tabstop=2 shiftwidth=2
  autocmd FileType text,markdown setlocal wrap linebreak sidescroll=5 listchars+=precedes:<,extends:>
  autocmd FileType text,markdown nnoremap j gj
  autocmd FileType text,markdown nnoremap k gk
  autocmd FileType text,markdown nnoremap 0 g0
  autocmd FileType text,markdown nnoremap $ g$
  autocmd FileType text,markdown nnoremap ^ g^
augroup END

function TrimEndLinesAndTrailingSpaces()
  let save_cursor = getpos(".")
  silent! %s#\($\n\s*\)\+\%$##
  silent! %s/\s\+$//e
  call setpos('.', save_cursor)
  " echom "trailing spaces and empty lines done!"
endfunction
augroup filetype_edit_behavior
  autocmd!
  autocmd FileType * setlocal textwidth=0
  " do not auto add comment when add new comment line in normal mode
  autocmd FileType * setlocal formatoptions-=o
  " auto remove all trailing empty lines before saving
  autocmd BufWritePre *.c,*.cpp,*.h,*.js,*.html,*.sh,*.py,*.yml,*.yaml
        \ call TrimEndLinesAndTrailingSpaces()
augroup END

augroup keymap_force
  autocmd!
  autocmd BufEnter * nnoremap <nowait><silent> <Space> <C-f>
augroup END

augroup windows_display
  autocmd!
  autocmd WinLeave * if &filetype!='help' && &filetype!='nerdtree' | setlocal norelativenumber number
  autocmd WinEnter * if &filetype!='help' && &filetype!='nerdtree' | setlocal relativenumber
augroup END

function! MarkdownFolds()
  let thisline = getline(v:lnum)
  if match(thisline, '^#\{6}[^#]*') >= 0
    return ">6"
  elseif match(thisline, '^#\{5}[^#]*') >= 0
    return ">5"
  elseif match(thisline, '^#\{4}[^#]*') >= 0
    return ">4"
  elseif match(thisline, '^#\{3}[^#]*') >= 0
    return ">3"
  elseif match(thisline, '^#\{2}[^#]*') >= 0
    return ">2"
  elseif match(thisline, '^#\{1}[^#]*') >= 0
    return ">1"
  else
    return "="
  endif
endfunction

function! MarkdownFoldText()
  let foldsize = (v:foldend-v:foldstart)
  return getline(v:foldstart).' ('.foldsize.' lines)'
endfunction
augroup filetype_fold
  autocmd!
  autocmd FileType markdown set foldmethod=expr foldexpr=MarkdownFolds() foldtext=MarkdownFoldText()
augroup END
" delete other buffers {{{
command! -nargs=? -complete=buffer -bang Bonly
      \ :call BufOnly('<args>', '<bang>')

function! BufOnly(buffer, bang)
  if a:buffer == ''
    " No buffer provided, use the current buffer.
    let buffer = bufnr('%')
  elseif (a:buffer + 0) > 0
    " A buffer number was provided.
    let buffer = bufnr(a:buffer + 0)
  else
    " A buffer name was provided.
    let buffer = bufnr(a:buffer)
  endif

  if buffer == -1
    echohl ErrorMsg
    echomsg "No matching buffer for" a:buffer
    echohl None
    return
  endif

  let last_buffer = bufnr('$')

  let delete_count = 0
  let n = 1
  while n <= last_buffer
    if n != buffer && buflisted(n)
      if a:bang == '' && getbufvar(n, '&modified')
        echohl ErrorMsg
        echomsg 'No write since last change for buffer'
              \ n '(add ! to override)'
        echohl None
      else
        silent exe 'bdel' . a:bang . ' ' . n
        if ! buflisted(n)
          let delete_count = delete_count+1
        endif
      endif
    endif
    let n = n+1
  endwhile

  if delete_count == 1
    echomsg delete_count "buffer deleted"
  elseif delete_count > 1
    echomsg delete_count "buffers deleted"
  endif

endfunction
" }}}
" }}}
" code compile and run {{{
" for detecting OS
if !exists("g:os")
  if has("win64") || has("win32") || has("win16")
    let g:os = "Windows"
  else
    let g:os = substitute(system('uname'), '\n', '', '')
  endif
endif

function! TermWrapper(command) abort
  if !exists('g:split_term_style') | let g:split_term_style = 'vertical' | endif
  if g:split_term_style ==# 'vertical'
    let buffercmd = 'vnew'
  elseif g:split_term_style ==# 'horizontal'
    let buffercmd = 'new'
  else
    echoerr 'ERROR! g:split_term_style is not a valid value (must be ''horizontal'' or ''vertical'' but is currently set to ''' . g:split_term_style . ''')'
    throw 'ERROR! g:split_term_style is not a valid value (must be ''horizontal'' or ''vertical'')'
  endif
  exec buffercmd
  if exists('g:split_term_resize_cmd')
    exec g:split_term_resize_cmd
  endif
  exec 'term ' . a:command
  exec 'setlocal norelativenumber nonumber'
  exec 'startinsert'
  autocmd BufEnter <buffer> startinsert
endfunction

function! CodeRun() abort
  exec 'wa'
  if filereadable('Makefile')
    nnoremap <C-c> :make clean<CR>
    call TermWrapper(printf('make && make run'))
    return
  endif
  if &filetype == "c"
    call TermWrapper(printf('gcc -Wall -std=c11 *.c && ./a.out'))
  elseif &filetype == "cpp"
    call TermWrapper(printf('g++ -Wall -std=c++11 *.cpp && ./a.out'))
  elseif &filetype == "python"
    call TermWrapper(printf('python %s', expand('%')))
  elseif &filetype == "java"
    call TermWrapper(printf('javac %s && java %s', expand('%'), expand('%:r')))
  elseif &filetype == "javascript"
    call TermWrapper(printf('node %s', expand('%')))
  elseif &filetype == "go"
    call TermWrapper(printf('go run %s', expand('%')))
  elseif &filetype == "typescript"
    call TermWrapper(printf('tsc %s && node %s', expand('%'), expand('%:r')))
  elseif &filetype == "rust"
    call TermWrapper(printf('cargo run'))
  elseif &filetype == "sh"
    call TermWrapper(printf("bash %s", expand('%')))
  else
    echo "Unsupported yet..."
  endif
endfunction

function CodeTest() abort
  exec 'wa'
  if &filetype == "rust"
    call TermWrapper(printf('cargo test'))
  elseif &filetype == "go"
    exec 'GoBuild'
  endif
endfunction

autocmd FileType c,cpp,java,python,javascript,typescript,go,rust,sh nnoremap <leader>r :call CodeRun()<CR>
autocmd FileType rust,go nnoremap <leader>t :call CodeTest()<CR>

command! -nargs=1 -complete=file CompileAndRunWithFile call TermWrapper(printf('g++ -std=c++11 %s && ./a.out < %s', expand('%'), <q-args>))

" }}}
" statueline {{{
set statusline=
set statusline+=%h
set statusline+=\ %f
function FileWritableSign()
  return filewritable(expand('%')) == 0 ? ' ' : ''
endfunction
function ModifiedSign()
  return &modified ? ' []' : ''
endfunction
set statusline+=%{FileWritableSign()}
set statusline+=%{ModifiedSign()}
set statusline+=%=
set statusline+=%6.50l/%-6.50L
set statusline+=%P
set statusline+=\ \ 
" }}}
" }}}
